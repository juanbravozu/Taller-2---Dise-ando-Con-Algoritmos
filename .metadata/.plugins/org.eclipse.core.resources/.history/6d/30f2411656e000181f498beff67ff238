package mquevedojbravo;

import java.util.Iterator;

import processing.core.PApplet;
import processing.core.PFont;
import processing.core.PImage;
import processing.core.PVector;

public class Ovni extends Personaje{

	private PApplet app;
	private Mundo m; 
	private Jugador j;
	private PImage ovni;
	private float ang;
	private PFont mali;
	
	public Ovni(PApplet app, Mundo m) {
		super(app);
		this.m = m;
		this.app = app;
		vel.set(m.getJ().getPos());
		mali = app.loadFont("maliB_18.vlw");
		
		int random = (int)app.random(4);
		float x, y;
		if(random == 0) {
			x = app.random(-200,-50);
			y = app.random(app.height);
		} else if(random == 1) {
			x = app.random(app.width+50, app.width+200);
			y = app.random(app.height);
		} else if(random == 2) {
			x = app.random(app.width);
			y = app.random(-200,-50);
		} else {
			x = app.random(app.width);
			y = app.random(app.height+50, app.height+200);
		}

		pos = new PVector(x, y);
		ovni = app.loadImage("ovni.png");
		
		velmax = 5;
		fmax = 0.2f;
		ang = 0;
		
		img = app.loadImage("ovni.png");
	}

	public void run() {
		while(vivo) {
			try {
				if(estrellas > m.getJ().getEstrellas()) {
					perseguirJugador();
				} else if(estrellas < m.getJ().getEstrellas()){					
					if(app.dist(pos.x, pos.y, m.getJ().getPos().x, m.getJ().getPos().y) < 250) {
						huir(m.getJ().getPos());
					} else {
						buscarObj();
					}
				} else {
					buscarObj();
				}
				actualizar();
				
				ang += app.PI/50;
				sleep(16);
			} catch (InterruptedException e) {
				// TODO Auto-generated catch block
				e.printStackTrace();
			}
		}
	}
	
	public void pintar() {
		app.pushMatrix();
		app.translate(pos.x, pos.y);
		app.fill(255);
		app.textFont(mali);
		app.text(estrellas, 35, 15);
		app.rotate(ang);
		app.image(img, 0, 0);
		app.popMatrix();
	}
	
	public void buscarObj() {
		Recogible o = null;
		Iterator<Recogible> it = m.getObjetos().iterator();
		if(it.hasNext()) {
			o = it.next();
		}
		while(it.hasNext()) {
			Recogible obj = it.next();
			if(app.dist(pos.x,pos.y, obj.getPos().x, obj.getPos().y) < app.dist(pos.x, pos.y, o.getPos().x, o.getPos().y)) {
				o = obj;
			}
		}
		if(o != null) {
			perseguir(o.getPos());
			if(app.dist(pos.x, pos.y, o.getPos().x, o.getPos().y) < 25) {
				estrellas++;
				m.getObjetos().remove(o);
			}
		}
	}
	
	public void huir(PVector obj) {
		PVector deseado = PVector.add(obj, pos);
		deseado.normalize();
		deseado.mult(velmax);
		PVector direccion = PVector.sub(deseado, vel);
		direccion.limit(fmax);
		aplicarFuerza(direccion);
	}
	
	public void perseguirJugador() {
		perseguir(m.getJ().getPos());
		if(app.dist(m.getJ().getPos().x, m.getJ().getPos().y, pos.x, pos.y) < 40) {
			if(m.getJ().getEstrellas() > 0) {
				if(m.getJ().getEstrellas()-5 >= 0) {
					m.getJ().setEstrella(m.getJ().getEstrellas()-5);
				} else {
					m.getJ().setEstrella(0);
				}
			}
		}
	}
}
